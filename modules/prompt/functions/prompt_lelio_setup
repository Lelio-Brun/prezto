# Load dependencies.
pmodload 'helper'

function prompt_lelio_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_lelio_pwd="$MATCH"
    unset MATCH
  else
    _prompt_lelio_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_lelio_git_info {
  if (( _prompt_lelio_precmd_async_pid > 0 )); then
    # Append Git status.
    if [[ -s "$_prompt_lelio_precmd_async_data" ]]; then
      alias typeset='typeset -g'
      source "$_prompt_lelio_precmd_async_data"
      _lelio_git_status="${git_info:+${(e)git_info[status]}}"
      unalias typeset
    fi

    # Reset PID.
    _prompt_lelio_precmd_async_pid=0

    # Redisplay prompt.
    zle && zle reset-prompt
  fi
}

function prompt_lelio_precmd_async {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
    typeset -p git_info >! "$_prompt_lelio_precmd_async_data"
  fi

  # Signal completion to parent process.
  kill -WINCH $$
}

function prompt_lelio_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_lelio_pwd

  # Define prompts.
  RPROMPT='${editor_info[overwrite]}%(?::%F{1}⚠%f)${VIM:+" %B%F{6}V%f%b"}'

  # Kill the old process of slow commands if it is still running.
  if (( _prompt_lelio_precmd_async_pid > 0 )); then
    kill -KILL "$_prompt_lelio_precmd_async_pid" &>/dev/null
  fi

  # Compute slow commands in the background.
  trap prompt_lelio_git_info WINCH
  prompt_lelio_precmd_async &!
  _prompt_lelio_precmd_async_pid=$!
}

function prompt_lelio_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)
  _prompt_lelio_precmd_async_pid=0
  _prompt_sorin_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/lelio-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_lelio_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format '%F{2}+%f'
  zstyle ':prezto:module:git:info:ahead' format '%F{14}➚%f'
  zstyle ':prezto:module:git:info:behind' format '%F{13}➘%f'
  zstyle ':prezto:module:git:info:branch' format '%F{8}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{6}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format '%F{1}-%f'
  zstyle ':prezto:module:git:info:modified' format '%F{3}*%f'
  zstyle ':prezto:module:git:info:position' format '%F{12}%p%f'
  zstyle ':prezto:module:git:info:renamed' format '%F{11}⟲%f'
  zstyle ':prezto:module:git:info:stashed' format '%F{15}⬕%f'
  zstyle ':prezto:module:git:info:unmerged' format '%F{9}⧎%f'
  zstyle ':prezto:module:git:info:untracked' format '%F{10}¤%f'
  zstyle ':prezto:module:git:info:keys' format \
    'status' ' $(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{9}%n%f%F{7}@%f%F{3}%m%f "}%F{4}${_prompt_lelio_pwd}%(!. %B%F{1}#%f%b.)${_lelio_git_status}${editor_info[keymap]} '
  RPROMPT=''
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_lelio_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'lelio'
}

prompt_lelio_setup "$@"
